BehaviorModel:
    'variables' '{'
    variables *= variable
    '}' 'end_variables'
    'checks' '{'
    check_nodes *= check_node
    '}' 'end_checks'
    'actions' '{'
    action_nodes *= action_node
    '}' 'end_actions'
    'root_node' root = node
;
    
    
variable:
    'variable' '{'
    is_local ?= 'local' name = ID
    ((model_as = 'VAR') | (model_as = 'FROZENVAR') | (model_as = 'DEFINE'))
    domain = variable_domain
    (variable_case_results += variable_case_result) ?
    'used_before_tick' used_before_tick = BOOL
    'used_after_tick' used_after_tick = BOOL
    '}' 'end_variable'
;
    
    variable_domain:
    ('[' min_val = INT ',' max_val = INT ']') | (boolean = 'BOOLEAN') | ('{' enums += ID[','] '}')
;
    
node:
    composite_node | decorator_node | leaf_node
;
    composite_node:
    'composite' '{'
    name = ID
    ((node_type = 'parallel' 'policy' (parallel_policy = 'success_on_all' | parallel_policy = 'success_on_one')) | (node_type = 'sequence') | (node_type = 'selector'))
    'memory' memory=BOOL
    'children' '{'
    children += node
    '}' 'end_children'
    '}' 'end_composite'
;
    
    decorator_node:
    'decorator' '{'
    name = ID
    node_type = 'X_is_Y'
    'X' x = status
    'Y' y = status
    'child' child = node 
    '}' 'end_decorator'
;
status:
    'success' | 'running' | 'failure'
;
    
    
    leaf_node:
    (leaf = [check_node]) | (leaf = [action_node])
;
    
    check_node:
    (node_type = 'check') '{'
    name = ID
    ('start_python_code' '{' python_code = STRING '}' 'end_python_code')?
    (condition = condition_statement)?
    '}' 'end_check'
;
    
    action_node:
    (node_type = 'action') '{'
    name = ID
    'init' '{'
    init_statements *= variable_statement
    '}' 'end_init'
    'update' '{'
    pre_update_statements *= variable_statement
    return_statement = return_statement
    post_update_statements *= variable_statement
    '}' 'end_update'
    '}' 'end_action'
;

    comparison_symbol:
    '==' | '<' | '>' | '>=' | '<=' | '!='
;
    
    variable_statement:
    'variable_statement' '{'
    is_local ?= 'local' variable = [variable]
    case_results *= variable_case_result
    default_result = variable_default_result
    '}' 'end_variable_statement'
;
    
    return_statement:
    'return_statement' '{'
    case_results *=  status_case_result
    default_result = status_default_result
    '}' 'end_return_statement'
;

    variable_case_result:
    ('start_python_code' '{' python_code = STRING '}' 'end_python_code')?
    ('case' '{' condition = condition_statement '}' 'end_case')
    'result' '{'
    values += code_statement[',']
    '}' 'end_result'
;
        
    variable_default_result:
    ('start_python_code' '{' python_code = STRING '}' 'end_python_code')?
    'result' '{'
    values += code_statement[',']
    '}' 'end_result'
;

    status_case_result:
    ('start_python_code' '{' python_code = STRING '}' 'end_python_code')?
    ('case' '{' condition = condition_statement '}' 'end_case')
    'result' '{'
    'success' can_success = BOOL
    'failure' can_failure = BOOL
    'running' can_running = BOOL
    '}' 'end_result'
;
    
    status_default_result:
    ('start_python_code' '{' python_code = STRING '}' 'end_python_code')?
    'result' '{'
    'success' can_success = BOOL
    'failure' can_failure = BOOL
    'running' can_running = BOOL
    '}' 'end_result'
;
    
    condition_statement:
    'comparison_operator' operator = comparison_symbol
    'left_hand_side' '{' left_hand_side = code_statement '}' 'end_left_hand_side'
    'right_hand_side' '{' right_hand_side = code_statement '}' 'end_right_hand_side'
;

    code_statement:
    fragments += code_fragment
;
    
    code_fragment:
    (code = BOOL) | (code = INT) | (code = STRING) | (is_local ?= 'local' variable = [variable])
;
    
    
