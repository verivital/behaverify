configuration {
    #{ Here you specify various configuration options, such as Hypersafety }#
}
enumerations {
    #{ here you specify the enumerations used in the model}#
    'left', 'right', 'up', 'down', 'no_action'
}
constants {
    #{ constants are used to synchronize values in a .tree file. }#
    #{ example := 0 }#
    #{ to use example elsewhere, simply type 'example' in place of any integer (quotes included) }#
    #{ min_val := 0, max_val := 19, number_of_obstacles := 2, max_path_length := 50, max_obstacle_size := 3 }# #{ctl_spec is slow, rest is reasonable}#
    min_val := 0, max_val := 29, number_of_obstacles := 2, max_path_length := 50, max_obstacle_size := 3 #{ctl_spec is slow, rest is reasonable }#
} end_constants

variables {
    #{ Variable declarations go here.
	it is suggested, but not required, that the order of declaration be
	Blackboard, Local, Environment.
	If variable B depends on variable A, then variable A MUST be declared before B.
    }#
    variable { bl drone_x VAR [min_val, max_val] assign{result{(loop, loop_var, [min_val, max_val] such_that True, loop_var)}}}
    variable { bl drone_y VAR [min_val, max_val] assign{result{(loop, loop_var, [min_val, max_val] such_that True, loop_var)}}}
    variable { bl path_point_reached_var VAR BOOLEAN assign{result{False}}}
    variable { bl current_action VAR {'left', 'right', 'up', 'down', 'no_action'} assign{result{'no_action'}}}
    variable { bl path_length VAR [0, max_path_length] assign{result{0}}}
    #{##########################################}#
    variable { env obstacles FROZENVAR [min_val, max_val] array (mult, number_of_obstacles, 2) iterative_assign, index_var
	condition {(eq, (mod, index_var, 2), 1)}
	assign {
	    result {
		(loop, loop_var, [min_val, max_val] such_that True,
		    (if,
			(and,
			    (eq, drone_x, (index, obstacles, constant_index (sub, index_var, 1))),
			    (eq, drone_y, loop_var)
			),
			(if, (eq, drone_y, max_val), min_val, max_val),
			loop_var
		    )
		)
	    }
	}
	assign{result{(loop, loop_var, [min_val, max_val] such_that True, loop_var)}}
    }
    variable { env obstacle_sizes FROZENVAR [0, max_obstacle_size] array number_of_obstacles iterative_assign, index_var
	assign {
	    result{
		0,
		(loop, loop_var, [1, max_obstacle_size] such_that True,
		    (if,
			(and,
			    (lte, drone_x, (index, obstacles, constant_index (mult, index_var, 2))),
			    (gte, drone_x, (sub, (index, obstacles, constant_index (mult, index_var, 2)), loop_var)),
			    (lte, drone_y, (index, obstacles, constant_index (add, (mult, index_var, 2), 1))),
			    (gte, drone_y, (sub, (index, obstacles, constant_index (add, (mult, index_var, 2), 1)), loop_var))
			),
			0,
			loop_var
		    )
		)
	    }
	}
    }
    variable { env executing_action VAR {'left', 'right', 'up', 'down', 'no_action'} assign{result{'no_action'}}}
} end_variables

environment_update {
} end_environment_update

checks {
    check {
	path_point_reached
	arguments {}
	read_variables {path_point_reached_var}
	condition {path_point_reached_var}
    }
    check {
	path_ended
	arguments {}
	read_variables {path_length}
	condition {(eq, path_length, 0)}
    }
} end_checks

environment_checks {
    #{ check environment nodes are defined here }#
    #{
	environment_check {
	    z_idk
	    arguments{}
	    read_variables {} end_read_variables
	    condition { z } end_condition
	} end_environment_check
    }#
} end_environment_checks

actions {
    action {
	new_path
	arguments{}
	local_variables {} end_local_variables
	read_variables {} end_read_variables
	write_variables {path_length } end_write_variables
	initial_values {} end_initial_values
	update {
	    read_environment {
		function_get_new_path
		condition {True}
		variable_statement {
		    path_length
		    assign {
			result {(loop, loop_var, [1, max_path_length] such_that True, loop_var)}
		    }
		}
	    }
	    return_statement {
		result { success} end_result
	    } end_return_statement
	} end_update
    } end_action
    action {
	next_path_point
	arguments{}
	local_variables {} end_local_variables
	read_variables {drone_x, drone_y} end_read_variables
	write_variables {path_point_reached_var, current_action} end_write_variables
	initial_values {} end_initial_values
	update {
	    variable_statement {
		path_point_reached_var
		assign {
		    result {False}
		}
	    }
	    read_environment {
		function_get_next_point
		condition {True}
		variable_statement {
		    current_action
		    assign {
			result {
			    (if,
				(or,
				    (eq, drone_x, min_val),
				    (loop, loop_var, [0, (sub, number_of_obstacles, 1)] such_that True,
					(and,
					    (lte, (sub, drone_x, 1), (index, obstacles, constant_index (mult, loop_var, 2))),
					    (gte, (sub, drone_x, 1), (sub, (index, obstacles, constant_index (mult, loop_var, 2)), (index, obstacle_sizes, constant_index loop_var))),
					    (lte, drone_y, (index, obstacles, constant_index (add, (mult, loop_var, 2), 1))),
					    (gte, drone_y, (sub, (index, obstacles, constant_index (add, (mult, loop_var, 2), 1)), (index, obstacle_sizes, constant_index loop_var)))
					)
				    )
				),
				'no_action',
				'left'
			    ),
			    (if,
				(or,
				    (eq, drone_x, max_val),
				    (loop, loop_var, [0, (sub, number_of_obstacles, 1)] such_that True,
					(and,
					    (lte, (add, drone_x, 1), (index, obstacles, constant_index (mult, loop_var, 2))),
					    (gte, (add, drone_x, 1), (sub, (index, obstacles, constant_index (mult, loop_var, 2)), (index, obstacle_sizes, constant_index loop_var))),
					    (lte, drone_y, (index, obstacles, constant_index (add, (mult, loop_var, 2), 1))),
					    (gte, drone_y, (sub, (index, obstacles, constant_index (add, (mult, loop_var, 2), 1)), (index, obstacle_sizes, constant_index loop_var)))
					)
				    )
				),
				'no_action',
				'right'
			    ),
			    (if,
				(or,
				    (eq, drone_y, min_val),
				    (loop, loop_var, [0, (sub, number_of_obstacles, 1)] such_that True,
					(and,
					    (lte, drone_x, (index, obstacles, constant_index (mult, loop_var, 2))),
					    (gte, drone_x, (sub, (index, obstacles, constant_index (mult, loop_var, 2)), (index, obstacle_sizes, constant_index loop_var))),
					    (lte, (sub, drone_y, 1), (index, obstacles, constant_index (add, (mult, loop_var, 2), 1))),
					    (gte, (sub, drone_y, 1), (sub, (index, obstacles, constant_index (add, (mult, loop_var, 2), 1)), (index, obstacle_sizes, constant_index loop_var)))
					)
				    )
				),
				'no_action',
				'down'
			    ),
			    (if,
				(or,
				    (eq, drone_x, max_val),
				    (loop, loop_var, [0, (sub, number_of_obstacles, 1)] such_that True,
					(and,
					    (lte, drone_x, (index, obstacles, constant_index (mult, loop_var, 2))),
					    (gte, drone_x, (sub, (index, obstacles, constant_index (mult, loop_var, 2)), (index, obstacle_sizes, constant_index loop_var))),
					    (lte, (add, drone_y, 1), (index, obstacles, constant_index (add, (mult, loop_var, 2), 1))),
					    (gte, (add, drone_y, 1), (sub, (index, obstacles, constant_index (add, (mult, loop_var, 2), 1)), (index, obstacle_sizes, constant_index loop_var)))
					)
				    )
				),
				'no_action',
				'up'
			    )
			}
		    }
		}
	    }
	    return_statement {
		result { success} end_result
	    } end_return_statement
	} end_update
    } end_action
    action {
	read_position
	arguments{}
	local_variables {} end_local_variables
	read_variables {current_action} end_read_variables
	write_variables { drone_x, drone_y, path_length, path_point_reached_var } end_write_variables
	initial_values {} end_initial_values
	update {
	    read_environment {
		function_get_position
		condition_variable {path_point_reached_var}
		condition {non_determinism, True}
		variable_statement {
		    drone_x
		    assign {
			case {(eq, current_action, 'left')} result { (max, min_val, (sub, drone_x, 1))}
			case {(eq, current_action, 'right')} result { (min, max_val, (add, drone_x, 1))}
			result { drone_x}
		    }
		}
		variable_statement {
		    drone_y
		    assign {
			case {(eq, current_action, 'down')} result { (max, min_val, (sub, drone_y, 1))}
			case {(eq, current_action, 'up')} result { (min, max_val, (add, drone_y, 1))}
			result { drone_y}
		    }
		}
		variable_statement {
		    path_length
		    assign {
			result {(max, 0, (sub, path_length, 1))}
		    }
		}
	    }
	    return_statement {
		result { success} end_result
	    } end_return_statement
	} end_update
    } end_action
    action {
	send_action
	arguments{}
	local_variables {} end_local_variables
	read_variables {current_action} end_read_variables
	write_variables {} end_write_variables
	initial_values {} end_initial_values
	update {
	    write_environment {
		function_send_action
		variable_statement {
		    executing_action
		    assign {
			result {current_action}
		    }
		}
	    }
	    return_statement {
		result { success} end_result
	    } end_return_statement
	} end_update
    } end_action
} end_actions

sub_trees {#{ subtrees go here. }#} end_sub_trees

tree {
    composite {
	drone_control parallel policy success_on_all
	children {
	    read_position {}
	    composite {
		path_sequence sequence
		children {
		    path_ended {}
		    new_path {}
		}
	    }
	    composite {
		point_sequence sequence
		children {
		    path_point_reached {}
		    next_path_point {}
		}
	    }
	    send_action {}
	}
    }
} end_tree

tick_prerequisite { (True) } end_tick_prerequisite

specifications {
    INVARSPEC {
	(not,
	    (or,
		(loop, loop_var, [0, (sub, number_of_obstacles, 1)] such_that True,
		    (and,
			(lte, drone_x, (index, obstacles, constant_index (mult, loop_var, 2))),
			(gte, drone_x, (sub, (index, obstacles, constant_index (mult, loop_var, 2)), (index, obstacle_sizes, constant_index loop_var))),
			(lte, drone_y, (index, obstacles, constant_index (add, (mult, loop_var, 2), 1))),
			(gte, drone_y, (sub, (index, obstacles, constant_index (add, (mult, loop_var, 2), 1)), (index, obstacle_sizes, constant_index loop_var)))
		    )
		)
	    )
	)
    }
    CTLSPEC {
	(implies,
	    (always_globally, (always_finally, path_point_reached_var)),
	    (always_globally, (always_finally, (eq, path_length at 1, 0)))
	)
    }
} end_specifications