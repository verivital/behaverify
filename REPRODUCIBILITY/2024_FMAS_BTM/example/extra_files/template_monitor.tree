configuration {
    #{ Here you specify various configuration options, such as Hypersafety }#
}
enumerations {
    #{ here you specify the enumerations used in the model}#
    'left', 'right', 'up', 'down', 'no_action', 'safe', 'unsafe', 'unknown'
}
constants {
    #{ constants are used to synchronize values in a .tree file. }#
    #{ example := 0 }#
    #{ to use example elsewhere, simply type 'example' in place of any integer (quotes included) }#
    REPLACE_CONSTANTS
} end_constants

variables {
    #{ Variable declarations go here.
	it is suggested, but not required, that the order of declaration be
	Blackboard, Local, Environment.
	If variable B depends on variable A, then variable A MUST be declared before B.
    }#
    variable { bl drone_x VAR [min_val, max_val] assign{result{(idiv, (add, min_val, max_val), 2)}}}
    variable { bl drone_y VAR [min_val, max_val] assign{result{(idiv, (add, min_val, max_val), 2)}}}
    variable { bl drone_speed VAR [1, 2] assign{result{1}}}
    variable { bl destination_x VAR [min_val, max_val] assign{result{drone_x}}}
    variable { bl destination_y VAR [min_val, max_val] assign{result{drone_y}}}
    variable { bl cell_changed_var VAR BOOLEAN assign{result{False}}}
    variable { bl current_action VAR {'left', 'right', 'up', 'down', 'no_action'} assign{result{'no_action'}}}
    variable { bl delta_x DEFINE INT assign{
	    case{(eq, current_action, 'left')} result{-1}
	    case{(eq, current_action, 'right')} result{1}
	    result{0}
	}
    }
    variable { bl delta_y DEFINE INT assign{
	    case{(eq, current_action, 'down')} result{-1}
	    case{(eq, current_action, 'up')} result{1}
	    result{0}
	}
    }
    variable { bl monitor_var VAR {'safe', 'unsafe', 'unknown'} assign{result{'unknown'}}}
    variable { bl fake_network DEFINE ENUM assign{
	    REPLACE_FAKE_NETWORK
	}
    }
    #{##########################################}#
    variable { env obstacles DEFINE INT static array (mult, number_of_obstacles, 2) iterative_assign, index_var
	REPLACE_OBSTACLES
	assign{result{0}}
    }
    variable { env obstacle_sizes DEFINE INT static array number_of_obstacles iterative_assign, index_var
	REPLACE_OBSTACLE_SIZES
	assign{result{0}}
    }
    variable { env executing_action VAR {'left', 'right', 'up', 'down', 'no_action'} assign{result{'no_action'}}}
} end_variables

environment_update {
} end_environment_update

monitors {
    monitor {
	collision_monitor
	(safe : 'safe')
	(unsafe : 'unsafe')
	(unknown : 'unknown')
	LTLSPEC {	    
	    (globally,
		(not,
		    (or,
			(loop, loop_var, [0, (sub, number_of_obstacles, 1)] such_that True,
			    (and,
				(lte, (max, min_val, (min, max_val, (add, drone_x, (mult, delta_x, 2)))), (index, obstacles, constant_index (mult, loop_var, 2))),
				(gte, (max, min_val, (min, max_val, (add, drone_x, (mult, delta_x, 2)))), (sub, (index, obstacles, constant_index (mult, loop_var, 2)), (index, obstacle_sizes, constant_index loop_var))),
				(lte, (max, min_val, (min, max_val, (add, drone_y, (mult, delta_y, 2)))), (index, obstacles, constant_index (add, (mult, loop_var, 2), 1))),
				(gte, (max, min_val, (min, max_val, (add, drone_y, (mult, delta_y, 2)))), (sub, (index, obstacles, constant_index (add, (mult, loop_var, 2), 1)), (index, obstacle_sizes, constant_index loop_var)))
			    )
			)
		    )
		)
	    )
	}
    }
    monitor {
	loop_monitor
	(safe : 'safe')
	(unsafe : 'unsafe')
	(unknown : 'unknown')
	LTLSPEC {
	    (globally,
		(and,
		    (implies, (eq, current_action, 'left'), (next, (neq, current_action, 'right'))),
		    (implies, (eq, current_action, 'right'), (next, (neq, current_action, 'left'))),
		    (implies, (eq, current_action, 'up'), (next, (neq, current_action, 'down'))),
		    (implies, (eq, current_action, 'down'), (next, (neq, current_action, 'up')))
		)
	    )
	}
    }
}

checks {
    check {
	cell_changed
	arguments {}
	read_variables {cell_changed_var, current_action}
	condition {(or, cell_changed_var, (eq, current_action, 'no_action'))}
    }
    check {
	not_at_destination
	arguments {}
	read_variables {destination_x, destination_y, drone_x, drone_y, current_action}
	condition {
	    (not,
		(or,
		    (and, (eq, destination_x, drone_x), (eq, destination_y, drone_y)),
		    (eq, current_action, 'no_action')
		)
	    )
	}
    }
} end_checks

environment_checks {
} end_environment_checks

actions {
    action {
	new_destination
	arguments{}
	local_variables {} end_local_variables
	read_variables {} end_read_variables
	write_variables {destination_x, destination_y } end_write_variables
	initial_values {} end_initial_values
	update {
	    read_environment {
		function_get_new_destination
		condition {True}
		variable_statement {
		    destination_x
		    assign {
			result {(loop, loop_var, [min_val, max_val] such_that True, loop_var)}
		    }
		}
		variable_statement {
		    destination_y
		    assign {
			result {(loop, loop_var, [min_val, max_val] such_that True, loop_var)}
		    }
		}
	    }
	    return_statement {
		result { success} end_result
	    } end_return_statement
	} end_update
    } end_action
    action {
	next_action
	arguments{}
	local_variables {} end_local_variables
	read_variables {fake_network, drone_x, drone_y, destination_x, destination_y} end_read_variables
	write_variables {cell_changed_var, current_action} end_write_variables
	initial_values {} end_initial_values
	update {
	    variable_statement {
		cell_changed_var
		assign {
		    result {False}
		}
	    }
	    variable_statement {
		current_action
		assign {result{fake_network}}
	    }
	    return_statement {
		result { success} end_result
	    } end_return_statement
	} end_update
    } end_action
    action {
	read_monitor
	arguments{}
	local_variables {} end_local_variables
	read_variables {drone_x, drone_y, current_action, delta_x, delta_y} end_read_variables
	write_variables { drone_speed, monitor_var } end_write_variables
	initial_values {} end_initial_values
	update {
	    variable_statement {
		drone_speed
		assign {result{2}}
	    }
	    monitor_statement {
		collision_monitor
		monitor_variable {monitor_var}
		test
		reset
	    }
	    variable_statement {
		drone_speed
		assign{case{(eq, monitor_var, 'unsafe')}result{1}result{drone_speed}}
	    }
	    monitor_statement {
		loop_monitor
		monitor_variable {monitor_var}
		commit
		reset_on_failure
	    }
	    variable_statement {
		drone_speed
		assign{case{(eq, monitor_var, 'unsafe')}result{1}result{drone_speed}}
	    }
	    return_statement {
		result { success} end_result
	    } end_return_statement
	} end_update
    } end_action
    action {
	read_position
	arguments{}
	local_variables {} end_local_variables
	read_variables {current_action, delta_x, delta_y, drone_speed} end_read_variables
	write_variables { drone_x, drone_y, cell_changed_var } end_write_variables
	initial_values {} end_initial_values
	update {
	    read_environment {
		function_get_position
		#{
		    condition_variable {cell_changed_var}
		    condition {non_determinism, True}
		}#
		condition {True}
		variable_statement {
		    cell_changed_var
		    assign {result{True}}
		}
		variable_statement {
		    drone_x
		    assign {
			result { (max, min_val, (min, max_val, (add, drone_x, (mult, delta_x, drone_speed))))}
		    }
		}
		variable_statement {
		    drone_y
		    assign {
			result { (max, min_val, (min, max_val, (add, drone_y, (mult, delta_y, drone_speed))))}
		    }
		}
	    }
	    return_statement {
		result { success} end_result
	    } end_return_statement
	} end_update
    } end_action
    action {
	send_action
	arguments{}
	local_variables {} end_local_variables
	read_variables {current_action} end_read_variables
	write_variables {} end_write_variables
	initial_values {} end_initial_values
	update {
	    write_environment {
		function_send_action
		variable_statement {
		    instant
		    executing_action
		    assign {
			result {current_action}
		    }
		}
	    }
	    return_statement {
		result { success} end_result
	    } end_return_statement
	} end_update
    } end_action
} end_actions

sub_trees {#{ subtrees go here. }#} end_sub_trees

tree {
    composite {
	drone_control sequence
	children {
	    read_position {} #{this just gets our position}#
	    cell_changed {} #{this checks if the cell changed OR we took no_action}#
	    composite {
		destination_selector selector
		children {
		    not_at_destination {} #{success if we are not at the destination AND we took no action.}#
		    new_destination {} #{if we reached the destination OR we took no action, find a new destination}#
		}
	    }
	    next_action {} #{pick an action}#
	    read_monitor {}
	    send_action {} #{send the action}#
	}
    }
} end_tree

tick_prerequisite { (True) } end_tick_prerequisite

specifications {
    LTLSPEC {
	(globally,
	    (not,
		(or,
		    (loop, loop_var, [0, (sub, number_of_obstacles, 1)] such_that True,
			(and,
			    (lte, drone_x, (index, obstacles, constant_index (mult, loop_var, 2))),
			    (gte, drone_x, (sub, (index, obstacles, constant_index (mult, loop_var, 2)), (index, obstacle_sizes, constant_index loop_var))),
			    (lte, drone_y, (index, obstacles, constant_index (add, (mult, loop_var, 2), 1))),
			    (gte, drone_y, (sub, (index, obstacles, constant_index (add, (mult, loop_var, 2), 1)), (index, obstacle_sizes, constant_index loop_var)))
			)
		    )
		)
	    )
	)
    }
    LTLSPEC {
	(globally,
	    (
		(finally,
		    (or,
			(and,
			    (eq, drone_x at 1, destination_x at 0),
			    (eq, drone_y at 1, destination_y at 0)
			),
			(loop, loop_var, [0, (sub, number_of_obstacles, 1)] such_that True,
			    (and,
				(lte, destination_x at 0, (index, obstacles, constant_index (mult, loop_var, 2))),
				(gte, destination_x at 0, (sub, (index, obstacles, constant_index (mult, loop_var, 2)), (index, obstacle_sizes, constant_index loop_var))),
				(lte, destination_y at 0, (index, obstacles, constant_index (add, (mult, loop_var, 2), 1))),
				(gte, destination_y at 0, (sub, (index, obstacles, constant_index (add, (mult, loop_var, 2), 1)), (index, obstacle_sizes, constant_index loop_var)))
			    )
			)
		    )
		)
	    )
	)
    }
} end_specifications