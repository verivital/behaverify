configuration{}
enumerations{}
constants{
    'x_max' := 7,
    'y_max' := 7
}

variables {
    variable { bl x VAR [0, 'x_max'] assign {result {0} }}
    variable { bl y VAR [0, 'y_max'] assign {result {0} } }
    variable { bl target_x VAR [0, 'x_max'] assign {result {0} } }
    variable { bl target_y VAR [0, 'y_max'] assign {result {0} } }
    variable { bl mission VAR BOOLEAN assign {result {False} } }
    variable { local saw_target VAR BOOLEAN assign {result {True} } }
    variable { env x_goal VAR [0, 'x_max'] assign {result {range, 0, 'x_max', True} } }
    variable { env y_goal VAR [0, 'y_max'] assign {result {range, 0, 'y_max', True} } }
    variable { env x_true VAR [0, 'x_max'] assign {result {range, 0, 'x_max', True} } }
    variable { env y_true VAR [0, 'y_max'] assign {result {range, 0, 'y_max', True} } }
    variable { env remaining_goals VAR [0, 3] assign {result {range, 0, 3, True} } }
}

environment_update {
    variable_statement {
	remaining_goals
	assign{
	    case { (and, (equal, x_goal, x_true), (equal, y_goal, y_true)) } end_case
	    result { (max, 0, (subtraction, remaining_goals, 1)) } 
	    result { remaining_goals } 
	}
    }
    variable_statement {
	x_goal
	assign{
	    case { (equal, 0, remaining_goals ) } end_case
	    result { x_goal } 
	    case { (and, (equal, x_goal, x_true), (equal, y_goal, y_true)) } end_case
	    result {range, 0, 'x_max', True} 
	    result { x_goal } 
	}
    }
    variable_statement {
	y_goal
	assign {
	    case { (equal, 0, remaining_goals) } end_case
	    result { y_goal } 
	    case { (and, (equal, x_goal, x_true), (equal, y_goal, y_true)) } end_case
	    result {range, 0, 'y_max', True} 
	    result { y_goal } 
	}
    }
}

checks {
    check {
	have_mission
	arguments{}
	read_variables { mission } end_read_variables
	condition { mission } end_condition
    } end_check
    check {
	target_reached
	arguments{}
	read_variables { x, y, target_x, target_y } end_read_variables
	condition { (and, (equal, x, target_x), (equal, y, target_y)) } end_condition
    } end_check
    check {
	x_too_small
	arguments{}
	read_variables { target_x, x } end_read_variables
	condition { (less_than, x, target_x) } end_condition
    } end_check
    check {
	x_too_big
	arguments{}
	read_variables { target_x, x } end_read_variables
	condition { (greater_than, x, target_x) } end_condition
    } end_check
    check {
	y_too_small
	arguments{}
	read_variables { target_y, y } end_read_variables
	condition { (less_than, y, target_y) } end_condition
    } end_check
    check {
	y_too_big
	arguments{}
	read_variables { target_y, y } end_read_variables
	condition { (greater_than, y, target_y) } end_condition
    } end_check
} end_checks

environment_checks {
} end_environment_checks

actions {
    action {
	get_mission
	arguments{}
	
	local_variables {} end_local_variables
	read_variables {} end_read_variables
	write_variables {mission, target_x, target_y} end_write_variables
	initial_values {
	} end_initial_values
	update {
	    read_environment {
		mission_func
		condition { True } end_condition
		variable_statement {target_x assign{result { x_goal }} }
		variable_statement {target_y assign{result { y_goal }} }
	    } end_read_environment
	    variable_statement {
		mission
		assign{result { True } }
	    } end_variable_statement
	    return_statement {
		result { success } 
	    } end_return_statement
	} end_update
    } end_action
    
    
    action {
	get_position
	arguments{}
	
	local_variables {} end_local_variables
	read_variables {} end_read_variables
	write_variables { x, y } end_write_variables
	initial_values {
	} end_initial_values
	update {
	    read_environment {
		position_func
		condition { True } end_condition
		variable_statement {x assign{result { x_true }} }
		variable_statement {y assign{result { y_true }} }
	    } end_read_environment
	    return_statement { result { success }  } end_return_statement
	} end_update
    } end_action
    
    action {
	clear_mission
	arguments{}
	local_variables {} end_local_variables
	read_variables {} end_read_variables
	write_variables { mission } end_write_variables
	initial_values {} end_initial_values
	update {
	    variable_statement {
		mission
		assign{
		    result { False } }
	    } end_variable_statement
	    return_statement { result { success }  } end_return_statement
	} end_update
    } end_action
    
    action {
	go_right
	arguments{}
	local_variables {} end_local_variables
	read_variables {} end_read_variables
	write_variables {} end_write_variables
	initial_values {} end_initial_values
	update {
	    write_environment {
		right_func
		variable_statement {
		    x_true assign {result { (min, 'x_max', (addition, x_true, 1)) } }
		}
	    } end_write_environment
	    return_statement { result { success }  } end_return_statement
	} end_update
    } end_action
    
    action {
	go_left
	arguments{}
	local_variables {} end_local_variables
	read_variables {} end_read_variables
	write_variables {} end_write_variables
	initial_values {} end_initial_values
	update {
	    write_environment {
		left_func
		variable_statement {
		    x_true assign{result { (max, 0, (subtraction, x_true, 1)) } }
		} 
	    } end_write_environment
	    return_statement { result { success }  } end_return_statement
	} end_update
    } end_action
    
    action {
	go_up
	arguments{}
	local_variables {} end_local_variables
	read_variables {} end_read_variables
	write_variables {} end_write_variables
	initial_values {} end_initial_values
	update {
	    write_environment {
		up_func
		variable_statement {
		    y_true assign{result { (min, 'y_max', (addition, y_true, 1)) } }
		} 
	    } end_write_environment
	    return_statement { result { success }  } end_return_statement
	} end_update
    } end_action
    
    action {
	go_down
	arguments{}
	local_variables {} end_local_variables
	read_variables {} end_read_variables
	write_variables {} end_write_variables
	initial_values {} end_initial_values
	update {
	    write_environment {
		down_func
		variable_statement {
		    y_true assign{result { (max, 0, (subtraction, y_true, 1)) } }
		} end_variable_statement
	    } end_write_environment
	    return_statement { result { success }  } end_return_statement
	} end_update
    } end_action
} end_actions

sub_trees {
}end_sub_trees
tree{
    composite {
	robot_control
	sequence
	children {
	    get_position {}
	    decorator {
		reset_completed_mission_FiS
		X_is_Y
		X failure
		Y success
		child {
		    composite {
			reset_completed_mission
			sequence
			children {
			    target_reached {}
			    clear_mission {}
			} end_children
		    } end_composite
		}
	    } end_decorator
	    composite {
		confirm_mission
		selector
		children {
		    have_mission {}
		    get_mission {}
		} end_children
	    } end_composite
	    composite {
		move_robot
		selector
		children {
		    composite {
			try_right
			sequence
			children {
			    x_too_small {}
			    go_right {}
			} end_children
		    } end_composite
		    composite {
			try_left
			sequence
			children {
			    x_too_big {}
			    go_left {}
			} end_children
		    } end_composite
		    composite {
			try_up
			sequence
			children {
			    y_too_small {}
			    go_up {}
			} end_children
		    } end_composite
		    composite {
			try_down
			sequence
			children {
			    y_too_big {}
			    go_down {}
			} end_children
		    } end_composite
		} end_children
	    } end_composite
	} end_children
    } end_composite
} end_tree

tick_prerequisite { (greater_than, remaining_goals, 0) } end_tick_prerequisite
specifications {
    LTLSPEC { (finally, (equal, remaining_goals -1, 0)) } end_LTLSPEC
    CTLSPEC { (always_finally, (equal, remaining_goals -1, 0)) } end_CTLSPEC
} end_specifications