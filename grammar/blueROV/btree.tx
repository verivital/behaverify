BehaviorModel:
	'system' name=ID';'
	simpleTypes *= SimpleType
	messageTypes *= MessageType
	rosTopics *= Topic
	bbVariables *= BBVar
	args *= Arg
	bbEvents *= BBEvent
	bbNodes *= BBNode
	checkNodes *= CheckNode
	taskNodes *= BehaviorNode
    'tree' '(' 'updatetime' '=' updatetime=FLOAT ',' 'timeout' '=' timeout=FLOAT ')' tree=BTree
;



SimpleType:
	'type' name=ID ';'
	;

MessageType:
	'message' name=ID package=ID
		  fields *= Field
    'end'(';')?
	;

Type:	SimpleType | MessageType
	;

Field:	type=[Type](array='['(count=INT)?']')?  name=ID ';'
	;

Topic:	'topic' type=[MessageType] name=ID topic_string=/([^\s;]+)/ ';'
	;

BBVar:
	'var' type=[Type] name=ID ('=' default=DefaultBBType)? (',' 'model_as' '(' model = ModelOptions ',' initial_value = Enumeration ')')? ';'
	;

DefaultBBType:
    value=DefaultType | ref=[Arg]
	;

BBEvent:
	'event' name=ID topic=[Topic]';'
	;

Arg:          
    'arg' type=[Type](array='['(count=INT)?']')? name=ID ('=' default=DefaultType)? ';'
	;

Param:          
    'arg' type=[Type](array='['(count=INT)?']')? name=ID ('=' default=DefaultBBType)? ';'
	;

DefaultType:
    BASE_TYPE | BaseArrayType
    ;

BaseArrayType:
    '[' values+=BASE_TYPE[','] ']'
	;

BBNode:
	'input' name=ID
    input_topic=[Topic] '->' topic_bbvar=[BBVar]
    ('vars' bb_vars *= [BBVar][','] ';')?
    args *= Param
		 ('comment' comment=STRING)?
    set_vars *= SetVar
    'end'(';')?
	;


CheckNode:
	'check' name=ID	bbvar=[BBVar] '==' default=BASE_TYPE ';'
	;
	
BehaviorNode:
	StdBehaviorNode |
	TaskNode
	;

StdBehaviorNode:
    type=[StdBehaviorType] name=ID ';'
	;

StdBehaviorType:
	'success' | 'failure' | 'running'
	;
	
TaskNode:
	'task' name=ID 
	('in' input_topics+=TopicArg[','] ';')?
	('out' output_topics+=TopicArg[','] ';')?
    ('vars' bb_vars *= [BBVar][','] ';')?
	args*=Param
	('comment' comment=STRING (';')?)?
    set_vars *= SetVar
    'return' return_status = StdBehaviorType
    'end'(';')?
	;
	
TopicArg:
	type=[Topic] name=ID
	;

BTree:	btree=BTreeNode
	;

BTreeNode:
	ParBTNode
	| SeqBTNode
	| SelBTNode
	| SIFBTNode
	| MonBTNode
	| TaskBTNode
	| TimerBTNode
    | CheckBTNode
	;

ChildNode:
    BTreeNode
	;
	
ParBTNode:
	'par' name=ID ('('cond=Status')')?'{'
	      nodes *= ChildNode
	 '}'
	 ;

SeqBTNode:
	'seq' name=ID ('('cond=Status')')?'{'
	      nodes *= ChildNode
	'}'
	;

SelBTNode:
	'sel' name=ID ('('cond=Status')')?'{'
	      nodes *= ChildNode
	'}'
	;

SIFBTNode:
	'do' name=ID '{'
	       'if' checks+=[CheckNode][',']
	       'then' '{'
		    	nodes *= ChildNode
			'}'
	'}'
	;

MonBTNode:
	'mon' mon+=[BBNode][',']
	;

TaskBTNode:
	'exec' task+=[BehaviorNode][',']
	;

TimerBTNode:
	'timer' name=ID '('duration=NUMBER')'	
	;

CheckBTNode:
    'chk' check+=[CheckNode][',']
    ;

Status: 
    'success' | 'failure' | 'running' | 'invalid'
    ;

BASE_TYPE:
	NUMBER | FLOAT | BOOL |  STRING
	;
Comment:
    /\/\/.*$/
    ;









CodeStatement:
    (constant = STRICTFLOAT) | (constant = INT) | (constant = BOOL) | (constant = STRING) |
    (variable = [BBVar]) |
    ('(' function_call = function ')') |
    ('(' CodeStatement = CodeStatement ')')
;
    
function:
    (function_name = 'abs' ',' value1 = CodeStatement) |
    (function_name = 'max' ',' value1 = CodeStatement ',' value2 = CodeStatement) |
    (function_name = 'min' ',' value1 = CodeStatement ',' value2 = CodeStatement) |
    (function_name = 'sin' ',' value1 = CodeStatement) |
    (function_name = 'cos' ',' value1 = CodeStatement) |
    (function_name = 'exp' ',' value1 = CodeStatement) |
    (function_name = 'tan' ',' value1 = CodeStatement) |
    (function_name = 'ln' ',' value1 = CodeStatement) |
    (function_name = 'not' ',' value1 = CodeStatement) |
    (function_name = 'and' ',' value1 = CodeStatement ',' value2 = CodeStatement) |
    (function_name = 'or' ',' value1 = CodeStatement ',' value2 = CodeStatement) |
    (function_name = 'xor' ',' value1 = CodeStatement ',' value2 = CodeStatement) |
    (function_name = 'xnor' ',' value1 = CodeStatement ',' value2 = CodeStatement) |
    (function_name = 'implies' ',' value1 = CodeStatement ',' value2 = CodeStatement) |
    (function_name = 'equivalent' ',' value1 = CodeStatement ',' value2 = CodeStatement) |
    (function_name = 'equal' ',' value1 = CodeStatement ',' value2 = CodeStatement) |
    (function_name = 'not_equal' ',' value1 = CodeStatement ',' value2 = CodeStatement) |
    (function_name = 'less_than' ',' value1 = CodeStatement ',' value2 = CodeStatement) |
    (function_name = 'greater_than' ',' value1 = CodeStatement ',' value2 = CodeStatement) |
    (function_name = 'less_than_or_equal' ',' value1 = CodeStatement ',' value2 = CodeStatement) |
    (function_name = 'greater_than_or_equal' ',' value1 = CodeStatement ',' value2 = CodeStatement) |
    (function_name = 'negative' ',' value1 = CodeStatement) |
    (function_name = 'addition' ',' value1 = CodeStatement ',' value2 = CodeStatement) |
    (function_name = 'subtraction' ',' value1 = CodeStatement ',' value2 = CodeStatement) |
    (function_name = 'multiplication' ',' value1 = CodeStatement ',' value2 = CodeStatement) |
    (function_name = 'division' ',' value1 = CodeStatement ',' value2 = CodeStatement) |
    (function_name = 'mod' ',' value1 = CodeStatement ',' value2 = CodeStatement) |
    (function_name = 'any' ',' values += CodeStatement[','])
;

Enumeration:
    INT | BOOL | STRING
;

ModelOptions:
    (is_bool = 'bool') | ('{' enums += Enumeration[','] '}') | ( '[' range_minimum = INT ',' range_maximum = INT ']')
;

UpdateStatement:
    'try(' condition = CodeStatement ',' update_value = CodeStatement ')' ','
;
    
SetVar:
    'set' '(' variable = [BBVar] ',' updates *= UpdateStatement default_update = CodeStatement')'
;
