BehaviorModel:
    'variables' '{'
    variables *= variable
    '}' 'end_variables'
    'local_variables' '{'
    local_variables *= variable
    '}' 'end_local_variables'
    'environment' '{'
    'environment_variables' '{'
    environment_variables *= environment_variable
    '}' 'end_environment_variables'
    'initial_values' '{'
    initial *= environment_statement
    '}' 'end_initial_values'
    'update_values' '{'
    update *= environment_statement
    '}' 'end_update_values'
    '}' 'end_environment'
    'checks' '{'
    check_nodes *= check_node
    '}' 'end_checks'
    'environment_checks' '{'
    environment_checks *= check_environment_node
    '}' 'end_environment_checks'
    'actions' '{'
    action_nodes *= action_node
    '}' 'end_actions'
    ('tick_prerequisite' '{' tick_condition = code_statement_env '}' 'end_tick_prerequisite')?
    'root_node' root = node
;

    variable_environment_pair:
    '(' is_local ?= 'local' variable = [variable] ',' 'env' environment_variable = [environment_variable] ')'
;
    
    read_statement:
    'read_environment' '{'
    ('python_function' '{' python_function = STRING '}' 'end_python_function')?
    (('condition' '{' condition = code_statement_env '}' 'end_condition') | (is_local = 'local' condition_variable = [variable]))
    'set_variables' '{' variable_environment_pairs *= variable_environment_pair[','] '}' 'end_set_variables'
    '}' 'end_read_environment'
;
    
    write_statement:
    'write_environment' '{'
    ('python_function' '{' python_function = STRING '}' 'end_python_function')?
    'update_values' '{'
    update *= environment_statement
    '}' 'end_update_values'
    '}' 'end_write_environment'
;

    environment_variable:
    'environment_variable' '{'
    name = ID
    ((model_as = 'VAR') | (model_as = 'FROZENVAR') | (model_as = 'DEFINE'))
    domain = variable_domain
    '}' 'end_environment_variable'
;
    
variable:
    'variable' '{'
    name = ID
    ((model_as = 'VAR') | (model_as = 'FROZENVAR') | (model_as = 'DEFINE'))
    domain = variable_domain
    '}' 'end_variable'
;
    
    variable_domain:
    ('[' min_val = INT ',' max_val = INT ']') | (boolean = 'BOOLEAN') | ('{' enums += enumeration[','] '}')
;
    
enumeration:
    INT | BOOL | STRING
;

status:
    'success' | 'running' | 'failure'
;
    
node:
    composite_node | decorator_node | leaf_node
;

    composite_node:
    'composite' '{'
    name = ID
    ((node_type = 'parallel' 'policy' (parallel_policy = 'success_on_all' | parallel_policy = 'success_on_one')) | (node_type = 'sequence') | (node_type = 'selector'))
    memory ?= 'with_memory'
    'children' '{'
    children += node
    '}' 'end_children'
    '}' 'end_composite'
;
    
    decorator_node:
    'decorator' '{'
    name = ID
    node_type = 'X_is_Y'
    'X' x = status
    'Y' y = status
    'child' child = node
    '}' 'end_decorator'
;

    
    
    leaf_node:
    (leaf = [check_node]) | (leaf = [action_node]) | (leaf = [check_environment_node])
;
    
    check_node:
    (node_type = 'check') '{'
    name = ID
    'read_variables' '{' read_variables *= [variable]  '}' 'end_read_variables'
    ('condition' '{' condition = code_statement '}' 'end_condition')
    '}' 'end_check'
;
    check_environment_node:
    (node_type = 'check_environment') '{'
    name = ID
    ('imports' '{' imports *= STRING[','] '}' 'end_imports')?
    ('python_function' '{' python_function = STRING '}' 'end_python_function')?
    ('condition' '{' condition = code_statement_env '}' 'end_condition')
    '}' 'end_check_environment'
;
    
    action_node:
    (node_type = 'action') '{'
    name = ID
    ('imports' '{' imports *= STRING[','] '}' 'end_imports')?
    'read_variables' '{' read_variables *= [variable]  '}' 'end_read_variables'
    'write_variables' '{' write_variables *= [variable]  '}' 'end_write_variables'
    'initial_values' '{'
    init_statements *= statement
    '}' 'end_initial_values'
    'update' '{'
    pre_update_statements *= statement
    return_statement = return_statement
    post_update_statements *= statement
    '}' 'end_update'
    '}' 'end_action'
;

statement:
    (variable_statement = variable_statement) | (read_statement = read_statement) | (write_statement = write_statement)
;

    environment_statement:
    'environment_statement' '{'
    instant ?= 'instant'
    'env' variable = [environment_variable]
    case_results *= environment_variable_case_result
    default_result = environment_variable_default_result
    '}' 'end_environment_statement'
;
    environment_variable_case_result:
    ('case' '{' condition = code_statement_env '}' 'end_case')
    'result' '{'
    values += code_statement_env[',']
    '}' 'end_result'
;
    environment_variable_default_result:
    'result' '{'
    values += code_statement_env[',']
    '}' 'end_result'
;
    
    variable_statement:
    'variable_statement' '{'
    is_local ?= 'local' variable = [variable]
    case_results *= variable_case_result
    default_result = variable_default_result
    '}' 'end_variable_statement'
;
    
    return_statement:
    'return_statement' '{'
    case_results *=  status_case_result
    default_result = status_default_result
    '}' 'end_return_statement'
;

    variable_case_result:
    ('case' '{' condition = code_statement '}' 'end_case')
    'result' '{'
    values += code_statement[',']
    '}' 'end_result'
;
        
    variable_default_result:
    'result' '{'
    values += code_statement[',']
    '}' 'end_result'
;

    status_case_result:
    ('case' '{' condition = code_statement '}' 'end_case')
    'result' '{'
    status = status
    '}' 'end_result'
;
    
    status_default_result:
    'result' '{'
    status = status
    '}' 'end_result'
;


    code_statement:
    (constant = STRICTFLOAT) | (constant = INT) | (constant = BOOL) | (constant = STRING) |
    (is_local ?= 'local' variable = [variable]) |
    ('(' function_call = function ')') |
    ('(' code_statement = code_statement ')')
;

    code_statement_env:
    (constant = STRICTFLOAT) | (constant = INT) | (constant = BOOL) | (constant = STRING) |
    (is_env = 'env' variable = [environment_variable] ) |
    (is_local ?= 'local' variable = [variable]) |
    ('(' function_call = function_env ')') |
    ('(' code_statement = code_statement_env ')')
;

function:
    (function_name = 'abs' ',' value1 = code_statement) |
    (function_name = 'max' ',' value1 = code_statement ',' value2 = code_statement) |
    (function_name = 'min' ',' value1 = code_statement ',' value2 = code_statement) |
    (function_name = 'sin' ',' value1 = code_statement) |
    (function_name = 'cos' ',' value1 = code_statement) |
    (function_name = 'exp' ',' value1 = code_statement) |
    (function_name = 'tan' ',' value1 = code_statement) |
    (function_name = 'ln' ',' value1 = code_statement) |
    (function_name = 'not' ',' value1 = code_statement) |
    (function_name = 'and' ',' value1 = code_statement ',' value2 = code_statement) |
    (function_name = 'or' ',' value1 = code_statement ',' value2 = code_statement) |
    (function_name = 'xor' ',' value1 = code_statement ',' value2 = code_statement) |
    (function_name = 'xnor' ',' value1 = code_statement ',' value2 = code_statement) |
    (function_name = 'implies' ',' value1 = code_statement ',' value2 = code_statement) |
    (function_name = 'equivalent' ',' value1 = code_statement ',' value2 = code_statement) |
    (function_name = 'equal' ',' value1 = code_statement ',' value2 = code_statement) |
    (function_name = 'not_equal' ',' value1 = code_statement ',' value2 = code_statement) |
    (function_name = 'less_than' ',' value1 = code_statement ',' value2 = code_statement) |
    (function_name = 'greater_than' ',' value1 = code_statement ',' value2 = code_statement) |
    (function_name = 'less_than_or_equal' ',' value1 = code_statement ',' value2 = code_statement) |
    (function_name = 'greater_than_or_equal' ',' value1 = code_statement ',' value2 = code_statement) |
    (function_name = 'negative' ',' value1 = code_statement) |
    (function_name = 'addition' ',' value1 = code_statement ',' value2 = code_statement) |
    (function_name = 'subtraction' ',' value1 = code_statement ',' value2 = code_statement) |
    (function_name = 'multiplication' ',' value1 = code_statement ',' value2 = code_statement) |
    (function_name = 'division' ',' value1 = code_statement ',' value2 = code_statement) |
    (function_name = 'mod' ',' value1 = code_statement ',' value2 = code_statement)
;

    function_env:
    (function_name = 'abs' ',' value1 = code_statement_env) |
    (function_name = 'max' ',' value1 = code_statement_env ',' value2 = code_statement_env) |
    (function_name = 'min' ',' value1 = code_statement_env ',' value2 = code_statement_env) |
    (function_name = 'sin' ',' value1 = code_statement_env) |
    (function_name = 'cos' ',' value1 = code_statement_env) |
    (function_name = 'exp' ',' value1 = code_statement_env) |
    (function_name = 'tan' ',' value1 = code_statement_env) |
    (function_name = 'ln' ',' value1 = code_statement_env) |
    (function_name = 'not' ',' value1 = code_statement_env) |
    (function_name = 'and' ',' value1 = code_statement_env ',' value2 = code_statement_env) |
    (function_name = 'or' ',' value1 = code_statement_env ',' value2 = code_statement_env) |
    (function_name = 'xor' ',' value1 = code_statement_env ',' value2 = code_statement_env) |
    (function_name = 'xnor' ',' value1 = code_statement_env ',' value2 = code_statement_env) |
    (function_name = 'implies' ',' value1 = code_statement_env ',' value2 = code_statement_env) |
    (function_name = 'equivalent' ',' value1 = code_statement_env ',' value2 = code_statement_env) |
    (function_name = 'equal' ',' value1 = code_statement_env ',' value2 = code_statement_env) |
    (function_name = 'not_equal' ',' value1 = code_statement_env ',' value2 = code_statement_env) |
    (function_name = 'less_than' ',' value1 = code_statement_env ',' value2 = code_statement_env) |
    (function_name = 'greater_than' ',' value1 = code_statement_env ',' value2 = code_statement_env) |
    (function_name = 'less_than_or_equal' ',' value1 = code_statement_env ',' value2 = code_statement_env) |
    (function_name = 'greater_than_or_equal' ',' value1 = code_statement_env ',' value2 = code_statement_env) |
    (function_name = 'negative' ',' value1 = code_statement_env) |
    (function_name = 'addition' ',' value1 = code_statement_env ',' value2 = code_statement_env) |
    (function_name = 'subtraction' ',' value1 = code_statement_env ',' value2 = code_statement_env) |
    (function_name = 'multiplication' ',' value1 = code_statement_env ',' value2 = code_statement_env) |
    (function_name = 'division' ',' value1 = code_statement_env ',' value2 = code_statement_env) |
    (function_name = 'mod' ',' value1 = code_statement_env ',' value2 = code_statement_env)
;
    
